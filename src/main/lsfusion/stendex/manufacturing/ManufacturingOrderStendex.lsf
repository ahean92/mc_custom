MODULE ManufacturingOrderStendex;

REQUIRE ManufacturingOrder, UtilsStendex, EmployeeStendex;

NAMESPACE Manufacturing;

link 'Ссылка' = DATA STRING (ConsumedLine) CHARWIDTH 25;

openLink 'Открыть ссылку' (ConsumedLine l) {
    open(LINK(link(l)));
}

orderBeforeDate 'Заказать до' = DATA DATE (ManufacturingOrder);
orderBeforeDate 'Заказать до' (ConsumedLine l) = orderBeforeDate(manufacturingOrder(l));

overNameResponsible 'Responsible' (ManufacturingOrder o) = OVERRIDE MasterData.overName(responsible(o)), MasterData.name(responsible(o));

FORM materialRequests 'Заявки'
    OBJECTS o = ManufacturingOrder
    PROPERTIES (o) READONLY date 'Дата' = scheduledDate, number, nameType, nameConsumedItem, nameStatus, overNameResponsible, orderBeforeDate
    PROPERTIES (o) NEWSESSION NEW, EDIT, DELETE

    PROPERTIES order 'Порядок' = VALUE(o)
    ORDERS order DESC
    
    LIST ManufacturingOrder OBJECT o
;

@defineDocObjectsForm(materialRequests, o, 'Заявки');
@defineDateFilterForm(materialRequests, o, scheduled);

FORM materialRequest 'Заявка'
    OBJECTS o = ManufacturingOrder PANEL
    PROPERTIES (o) scheduledDateTime, number, orderBeforeDate
    PROPERTIES (o) READONLY nameType, nameStatus, overNameResponsible

    OBJECTS cl = ConsumedLine
    PROPERTIES (cl) NOSELECT idItem, nameItem, nameUom, description, link, 'Количество' = toConsume
    PROPERTIES(cl) NOSELECT openLink READONLYIF NOT link(cl) AFTER link(cl) GRID
    PROPERTIES (cl) NEW, EDIT, DELETE
    FILTERS manufacturingOrder(cl) = o

    OBJECTS manufacturingOrderl = ManufacturingOrderHistory
    PROPERTIES(manufacturingOrderl) READONLY dateTime, nameUser, hostnameComputer, type, description, pdescription = description PANEL
    FILTERS manufacturingOrder(manufacturingOrderl) = o

    EDIT ManufacturingOrder OBJECT o
;

DESIGN materialRequest{
    OBJECTS {
        NEW order{
            NEW main{
                horizontal = TRUE;
                MOVE PROPERTY (nameType(o));
                MOVE PROPERTY (nameStatus(o));
            }
            NEW extra{
                horizontal = TRUE;
                MOVE PROPERTY (number(o));
                MOVE PROPERTY (scheduledDateTime(o));
                MOVE PROPERTY (overNameResponsible(o));
                MOVE PROPERTY (orderBeforeDate(o));
            }
        }
        NEW details{
            tabbed = TRUE;
            fill = 1;
            MOVE BOX(cl) {
                GRID(cl){
                    height = 150;
                    PROPERTY (description(cl)) {charWidth = 20; }
                    PROPERTY (nameItem(cl)) {charWidth = 16; }
                }
                caption = 'Материалы';
            }
            NEW history {
                caption = 'History';
                fill = 1;
                
                MOVE BOX(manufacturingOrderl) {
                    fill = 2;
                    caption = '';
                    PROPERTY(description(manufacturingOrderl)) { valueHeight = 18; }
                }
                MOVE PROPERTY(pdescription) { panelCaptionVertical = TRUE; fill = 1; }
            }
        }
    }
}



loadDefaultData () + {
    NEW t = ManufacturingOrderType {
        name(t) <- 'Заявка на материалы';
        id(t) <- 'materialRequest';
        NEW n = Numerator {
            name(n) <- 'Заявка на материалы';
            series(n) <- 'ЗМ';
            minValue(n) <- 1;
            maxValue(n) <- 999999;
            stringLength(n) <- 6;
            numerator(t) <- n;
        }
    }
}

WHEN LOCAL SET (ManufacturingOrder t IS ManufacturingOrder) AND NOT type(t) DO type(t) <- manufacturingOrderType('materialRequest');


NAVIGATOR{
    dashboard {
        NEW materialRequests;
    }
}

showLines 'Показать состав' = DATA BOOLEAN (CustomUser);
showLines 'Показать состав' = showLines(currentUser());

EXTEND FORM materialRequests
    PROPERTIES showLines() TOOLBAR DRAW o ON CHANGE {
        NEWSESSION APPLY {
            showLines(currentUser()) <- NOT showLines(currentUser());
        }
    }

    OBJECTS cl = ConsumedLine
    PROPERTIES(cl) READONLY SHOWIF showLines() idItem, nameItem, nameUom, description, 'Количество' = toConsume
    PROPERTIES(cl) SHOWIF showLines() link ON CHANGE { open(link(cl)); } AFTER description(cl)
    FILTERS manufacturingOrder(cl) = o
;

DESIGN materialRequests {
    tabbedPane {
        NEW orders {
            caption = 'Заявки';
            MOVE BOX(o) {
                TOOLBARBOX(o) {
                    MOVE PROPERTY(showLines()) BEFORE TOOLBARRIGHT(o);
                }
            }
            MOVE BOX(cl);
        }
    }
}