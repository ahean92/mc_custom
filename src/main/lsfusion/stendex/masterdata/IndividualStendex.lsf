MODULE IndividualStendex;

REQUIRE Individual;

NAMESPACE MasterData;

CONSTRAINT SET(LegalEntity l IS LegalEntity) AND NOT [GROUP MIN Individual i BY legalEntity(i)](l)
    MESSAGE 'Для созданного контрагента отсутствуют контакты';

DESIGN partner {
    tabs {
        MOVE BOX(em) FIRST;
    }
}

similar = FORMULA NUMERIC[1,2] 'similarity($1,$2)';

FORM similarIndividuals 'Список похожих физ. лиц'
    OBJECTS p = Individual PANEL
    
    OBJECTS i = Individual
    PROPERTIES READONLY name(i), firstName(i), lastName(i)
    FILTERS similar(name(p), name(i)) > 0.5
;

EXTEND FORM partner
    EVENTS ON APPLY BEFORE {
        IF p IS Individual AND (GROUP SUM 1 IF similar(name(p), name(Individual i)) > 0.5 AND i IS Individual AND NOT i == p) THEN {
            ASK (CONCAT '', 'В базе уже есть физ. лица с похожим именем: \n',
                (GROUP CONCAT name(Individual i) IF i IS Individual AND similar(name(p), name(i)) > 0.5 AND NOT i == p,  '\n' ORDER i) + '\n',
                'Вы действительно хотите сохранить?') DO {
                APPLY;
            } ELSE {
                beforeCanceled() <- TRUE;
            }
        }
    }
;

    