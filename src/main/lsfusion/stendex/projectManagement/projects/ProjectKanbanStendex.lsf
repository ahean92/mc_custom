MODULE ProjectKanbanStendex;

REQUIRE Project, ProjectDashboardStendex;

PRIORITY MasterData;

NAMESPACE ProjectManagement;

color 'Цвет' = DATA STRING (ProjectStatus);
EXTEND FORM projectStatus PROPERTIES(o) color;

budget (ProjectStatus s) = GROUP SUM budget(Project p) IF status(p) = s AND [FILTER projects.p](p);

projectStatuses () = JSON FROM id = ProjectStatus s, name(s), color(s), budget = toChar(budget(s), '999 999 999 ₽') WHERE s IS ProjectStatus AND NOT closed(s);
projectEmployees () = JSON FROM id = Employee e, name(e) WHERE e IS Employee;

order = DATA LONG (CustomUser, Project);
currentOrder (Project t) =  order(currentUser(), t);

createProject() {
    INPUT id = LONG DO {
        NEW tt = Project {
            FOR LONG(ProjectStatus s AS ProjectStatus) = id DO
                status(tt) <- s;
            order(currentUser(), tt) <- -LONG(tt);
        }
    }
}

budgetString (Project p) = toChar(budget(p), '999 999 999 ₽');
interval (Project p) = CONCAT ' - ', toChar(startDate(p), 'DD Mon YY'), toChar(endDate(p), 'DD Mon YY');

editAllProjects 'Редактировать все проекты' =  DATA BOOLEAN (UserRole);
editAllProjects (User u) = GROUP SUM 1 IF editAllProjects(UserRole r) AND has(u, r);

EXTEND FORM securityPolicy PROPERTIES(ur) PANEL editAllProjects;
DESIGN securityPolicy {
    roleApplicationSettings { MOVE PROPERTY(editAllProjects(ur)); }
}

readonlyCurrentUser(Project p) = NOT manager(p) = currentUser() AND NOT editAllProjects(currentUser()); 

EXTEND FORM projects
    OBJECTS pp = Project CUSTOM 'projectKanban'
    OPTIONS JSON FROM employees = projectEmployees(), statuses = projectStatuses()
    
    PROPERTIES(pp)
        readonly = readonlyCurrentUser,
        namePartner,
        budget = budgetString,
        installDate = interval,
        deinstallDate = interval,
        runDate = interval,
        nameExhibition = name,
        nameManager,
        status ON CHANGE {
            INPUT l = LONG DO
                FOR LONG(ProjectStatus ts AS ProjectStatus) = l DO
                    status(pp) <- ts;
                    ELSE
                status(pp) <- NULL;
        },
        currentOrder ON CHANGE {
            INPUT ord = INTEGER DO order(currentUser(), pp) <- ord;
        }

    ORDERS status(pp), currentOrder(pp)

    FILTERS [FILTER projects.p](pp)

    PROPERTIES(pp) READONLYIF readonlyCurrentUser(p) PANEL pnamePartner = namePartner, pbudget = budget, pname = name,
        pstartDate = startDate, pendDate = endDate, pnameManager = nameManager, pdescription = description,
        edit = EDIT
;

DESIGN projects {
    tabbedPane {
        NEW kanban {
            caption = 'Kanban';
            MOVE GRID(pp) {
                size = (500, 300);
                fill = 1;
                MOVE PROPERTY(pnamePartner) { alignment = STRETCH; }
                MOVE PROPERTY(pbudget) { alignment = STRETCH; }
                MOVE PROPERTY(pname) { alignment = STRETCH; }
                MOVE PROPERTY(pstartDate) { alignment = STRETCH; }
                MOVE PROPERTY(pendDate) { alignment = STRETCH; }
                MOVE PROPERTY(pnameManager) { alignment = STRETCH; }
                MOVE PROPERTY(pdescription) {
                    size = (250, 120);
                    panelCaptionVertical = TRUE;
                    alignment = STRETCH;
                }
                MOVE PROPERTY(edit) { alignment = STRETCH; }
            }
            REMOVE BOX(pp);
        }
    }
}

// change interval
onWebClientInit() + {
    onWebClientInit('dragula.js') <- 1;
    onWebClientInit('dragula.css') <- 1;
    onWebClientInit('projectkanban.js') <- 2;
    onWebClientInit('projectkanban.css') <- 2;
}

projectFilters = DATA LOCAL BOOLEAN ();
switchProjectFilters() { projectFilters() <- NOT projectFilters(); }
EXTEND FORM projects
    PROPERTIES () switchProjectFilters
;

DESIGN projects {
    TOOLBARRIGHT {
        MOVE PROPERTY (switchProjectFilters()) {
            caption = IF showPanel() THEN 'Скрыть фильтры' ELSE 'Фильтры';
        }
    }
    rightPane { showIf = projectFilters(); }
}
