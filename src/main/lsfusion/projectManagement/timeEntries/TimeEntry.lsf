MODULE TimeEntry;

REQUIRE Project, Employee;

PRIORITY MasterData;

NAMESPACE ProjectManagement;

CLASS TimeEntry 'Time entry';

date 'Date' = DATA DATE (TimeEntry);
date(TimeEntry e) <- currentDate() WHEN SET(e IS TimeEntry);

hours 'Hours' = DATA NUMERIC[8,2] (TimeEntry);

employee 'Employee' = DATA Employee (TimeEntry) NONULL;
nameEmployee 'Employee' (TimeEntry t) = name(employee(t));
WHEN SET(TimeEntry t IS TimeEntry) AND NOT employee(t) AND currentUser() IS Employee DO employee(t) <- currentUser();

description 'Description' = DATA ISTRING (TimeEntry) CHARWIDTH 20;

FORM timeEntry 'Time entry'
    OBJECTS t = TimeEntry PANEL
    PROPERTIES(t) nameEmployee, date, hours, description
    
    EDIT TimeEntry OBJECT t
;

DESIGN timeEntry {
    OBJECTS {
        NEW pane {
            NEW header {
                alignment = STRETCH;
                MOVE PROPERTY(nameEmployee(t));
            }
            NEW time {
                alignment = STRETCH;
                horizontal = TRUE;
                MOVE PROPERTY(date(t));
                MOVE PROPERTY(hours(t));
            }
            MOVE PROPERTY(description(t)) { alignment = STRETCH; charWidth = 40; }
        }
    }
}

FORM timeEntries 'Time entries'
    OBJECTS t = TimeEntry
    PROPERTIES(t) READONLY   nameEmployee, date, hours
    PROPERTIES(t) NEWSESSION NEW, EDIT, DELETE
    
    LIST TimeEntry OBJECT t
;

NAVIGATOR {
    operations {
        NEW timeEntries;
    }
}