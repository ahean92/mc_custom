MODULE BillStendex;

REQUIRE BillDone, BillCanceled, ProjectBill, ServiceP, UtilsStendex, EmployeeStendex, PartnerStendex;

NAMESPACE Invoicing;

isUrgent 'Срочный' = DATA BOOLEAN (Bill);
includeVAT 'С НДС' = DATA BOOLEAN (Bill);

overNameRepresentative 'Our representative' (Bill o)= OVERRIDE MasterData.overName(representative(o)), MasterData.name(representative(o));

EXTEND FORM bill
    PROPERTIES(b) READONLYIF readonly(b) overNameRepresentative
;
DESIGN bill {
    billInformat {
        MOVE PROPERTY(overNameRepresentative(b)) AFTER PROPERTY(nameRepresentative(b));
        REMOVE PROPERTY(nameRepresentative(b));
    }
}

forLegalEntity 'Для организаций' = DATA BOOLEAN (BillType);
legalEntityType () = GROUP MIN BillType t IF forLegalEntity(t);

forIndividual 'Для фрилансеров' = DATA BOOLEAN (BillType);
individualType () = GROUP MIN BillType t IF forIndividual(t);

defaultItem = DATA Item (BillType);
nameDefaultItem 'Услуга по умолчанию' (BillType t) = name(defaultItem(t));

billService 'Счет на услуги' = DATA BOOLEAN (BillType);

EXTEND FORM billType
    PROPERTIES(o) forLegalEntity, forIndividual, nameDefaultItem, billService
;

legalEntity (Bill b) = forLegalEntity(type(b));
individual (Bill b) = forIndividual(type(b));

CONSTRAINT SETCHANGED(vendor(Bill b)) AND vendor(b) IS LegalEntity AND NOT legalEntity(b) CHECKED BY vendor[Bill]
    MESSAGE 'Запрещено выбирать не организацию для этого типа счета';
CONSTRAINT SETCHANGED(vendor(Bill b)) AND vendor(b) IS Individual AND NOT individual(b) CHECKED BY vendor[Bill]
    MESSAGE 'Запрещено выбирать не физическое лицо для этого типа счета';
CONSTRAINT SETCHANGED(vendor(Bill b)) AND vendor(b) IS Employee CHECKED BY vendor[Bill]
    MESSAGE 'Запрещено выбирать сотрудника для этого типа счета';

WHEN LOCAL SETCHANGED(type(BillLine l)) AND NOT item(l) DO
    item(l) <- defaultItem(type(l));

FORM serviceCustom 'Услуга'
    OBJECTS s = Service PANEL
    PROPERTIES(s) dataName
//    EDIT Service OBJECT s
;

DESIGN serviceCustom {
    PROPERTY(dataName(s)) { charWidth = 50; }
}

countServices (Category c) = GROUP SUM 1 IF category(Item i) = c AND i IS Service;

addServiceCustom 'Добавить' () {
    NEW s = Service {
        category(s) <- GROUP LAST Category c ORDER DESC c WHERE countServices(c); 
        SHOW serviceCustom OBJECTS s = s;
        IF PREV(s IS Service) THEN seek(s);
    }
} IMAGE 'add.png' CHANGEKEY 'INSERT' HIDE TOOLBAR;

editServiceCustom 'Редактировать' (Service s) {
    SHOW serviceCustom OBJECTS s = s;
} IMAGE 'edit.png' CHANGEKEY 'BACK_SPACE' HIDE CHANGEMOUSE 'DBLCLK' TOOLBAR;

FORM servicesCustom 'Услуги'
    OBJECTS s = Service
    PROPERTIES(s) READONLY name
    PROPERTIES NEWSESSION addServiceCustom() DRAW s, editServiceCustom(s)
;

changeItems (Bill b) {
    DIALOG servicesCustom OBJECTS s INPUT LIST name(s) FILTERS s IS Service DO {
        IF NOT GROUP SUM 1 IF b = bill(BillLine bl) AND NOT item(bl) IS Product THEN 
            NEW l = BillLine {
                bill(l) <- b;
                item(l) <- s;
            }
        ELSE
            item(BillLine l) <- s WHERE bill(l) = b AND NOT item(l) IS Product;
    }
}

changeAmount (Bill b) {
    INPUT a = amount(b) DO {
        IF NOT countServiceLine(b) THEN
            NEW l = BillLine {
                bill(l) <- b;
                untaxedAmount(l) <- a;
            }
        ELSE
            untaxedAmount(BillLine l) <- a WHERE bill(l) = b AND NOT item(l) IS Product;
    }
}

showAllInvoices 'Показывать все счета' = DATA BOOLEAN (UserRole);
showAllInvoices (User u) = GROUP SUM 1 IF showAllInvoices(UserRole r) AND has(u, r);
editAllInvoices 'Редактировать закрытые счета' =  DATA BOOLEAN (UserRole);
editAllInvoices (User u) = GROUP SUM 1 IF editAllInvoices(UserRole r) AND has(u, r);

EXTEND FORM securityPolicy
    PROPERTIES(ur) PANEL showAllInvoices, editAllInvoices
;
DESIGN securityPolicy {
    roleApplicationSettings {
        tabbed = TRUE;
        NEW bill {
            caption = 'Bill';
            lines = 3;
            MOVE PROPERTY(showAllInvoices(ur));
            MOVE PROPERTY(editAllInvoices(ur));
        }
    }
}

overNameVendor 'Vendor' (Bill b) = overName(vendor(b));

editingOn 'Редактировать' = DATA LOCAL BOOLEAN (Bill);
WHEN LOCAL SET(Bill b IS Bill) DO editingOn(b) <- TRUE;

readonly(Bill b) += WHEN b IS Bill AND NOT editingOn(b) THEN TRUE;

FORM billCustom 'Счет'
    OBJECTS b = Bill PANEL
    PROPERTIES(b) READONLYIF readonly(b)
                  imagedNameStatus,
                  'Услуга' = items ON CHANGE changeItems(b),
                  nameCompany,
                  nameProject,
                  overNameVendor,
                  note,
                  amount ON CHANGE changeAmount(b),
                  details,
                  isUrgent, includeVAT

    PROPERTIES(b) ready, done, canceled, cancel SHOWIF status(b) = BillStatus.draft

PROPERTIES(b) editingOn DISABLEIF ready(b) AND NOT editAllInvoices(currentUser())
;

DESIGN billCustom {
    OBJECTS {
        NEW statusPane {
            caption = 'Statuses';
            horizontal = TRUE;
            alignment = STRETCH;
            NEW statusActions {
                horizontal = TRUE;
                MOVE PROPERTY(editingOn(b));
                MOVE PROPERTY(cancel(b));
            }
            NEW status {
                flex = 1;
                NEW statusInfo {
                    alignment = END;
                    horizontal = TRUE;
                    MOVE PROPERTY(imagedNameStatus(b));
                    MOVE PROPERTY(ready(b));
                    MOVE PROPERTY(done(b));
                    MOVE PROPERTY(canceled(b));
                }
            }
        }
        NEW header {
            horizontal = TRUE;
            alignment = STRETCH;
            NEW headerLeft {
                lines = 2;
                MOVE PROPERTY(items(b)) { charWidth = 40; fontSize = 24; }
                MOVE PROPERTY(nameCompany(b)) { charWidth = 40; fontSize = 24; }
                MOVE PROPERTY(nameProject(b)) { charWidth = 40; fontSize = 24; }
                MOVE PROPERTY(overNameVendor(b)) { charWidth = 40; fontSize = 24; }
                MOVE PROPERTY(note(b)) { fontSize = 24; }
                MOVE PROPERTY(amount(b)) { fontSize = 24; }
                MOVE PROPERTY(isUrgent(b));
                MOVE PROPERTY(includeVAT(b));
            }
        }
        NEW details {
            tabbed = TRUE;
            fill = 1;
        }
    }
}

@defineObjectFilesForm(bill, billCustom, b);
@defineDocHistoryForm(bill, billCustom, b);

CLASS TypeFile{
    paymentOrder 'Платежное поручение',
    invoice 'Счет',
    receipt 'Чек на оплату',
    other 'Другое'
}
lockChangeType = DATA BOOLEAN (BillFile);
type = DATA TypeFile (BillFile) NONULL;
nameType 'Тип' (BillFile f) = staticCaption(type(f));

WHEN LOCAL SET(BillFile f IS BillFile) AND NOT type(f) DO type(f) <- TypeFile.other;

uploadFile 'Загрузить файл'(Bill b){
    INPUT uf = NAMEDFILE DO NEW uof = BillFile {
        bill(uof) <- b;
        file(uof) <- RAWFILE(uf);
        name(uof) <- name(uf);
        extension(uof) <- extension(uf);
    }
} IMAGE 'add.png';

uploadInvoice 'Счет' (Bill b){
    INPUT uf = NAMEDFILE DO NEW uof = BillFile {
        bill(uof) <- b;
        file(uof) <- RAWFILE(uf);
        name(uof) <- name(uf);
        extension(uof) <- extension(uf);
        type(uof) <- TypeFile.invoice;
        lockChangeType(uof) <- TRUE;
    }
} IMAGE 'add.png';


uploadPaymentOrder 'Платежное поручение' (Bill b){
    INPUT uf = NAMEDFILE DO NEW uof = BillFile {
        bill(uof) <- b;
        file(uof) <- RAWFILE(uf);
        name(uof) <- name(uf);
        extension(uof) <- extension(uf);
        type(uof) <- TypeFile.paymentOrder;
        lockChangeType(uof) <- TRUE;
    }
} IMAGE 'add.png';


countPaymentOrders(Bill b) = GROUP SUM 1 IF type(BillFile f) = TypeFile.paymentOrder AND bill(f) = b;
countInvoices(Bill b) = GROUP SUM 1 IF type(BillFile f) = TypeFile.invoice AND bill(f) = b;

CONSTRAINT LOCAL SETCHANGED (status(Bill b)) AND status(b) = BillStatus.ready AND NOT countInvoices(b)
    MESSAGE 'Для перевода в статус "К оплате" необходимо прикрепить счет!';

CONSTRAINT LOCAL SETCHANGED (status(Bill b)) AND status(b) = BillStatus.done AND NOT countPaymentOrders(b)
    MESSAGE 'Для перевода в статус "Оплачено" необходимо прикрепить платежное поручение!';


EXTEND FORM billCustom
    PROPERTIES TOOLBAR DRAW of uploadInvoice(b) READONLYIF readonly(b), uploadPaymentOrder(b) SHOWIF status(b) != BillStatus.draft
    PROPERTIES nameType(of) FIRST READONLYIF lockChangeType(of) OR readonly(b)
;

DESIGN billCustom {
    details {
        NEW detailsInformation {
            caption = 'Details';
            MOVE PROPERTY(details(b)) {
                caption = '';
                fill = 1;
                height = 200;
            }
        }
    }
}

newLegalEntityBill 'Добавить' () {
    NEWSESSION {
        NEW b = Bill {
            type(b) <- legalEntityType(); 
            SHOW billCustom OBJECTS b = b;
        }
    }
}

newIndividualBill 'Добавить' () {
    NEWSESSION {
        NEW b = Bill {
            type(b) <- individualType();
            SHOW billCustom OBJECTS b = b;
        }
    }
}

editBillCustom 'Редактировать' (Bill b) {
    SHOW billCustom OBJECTS b = b;
} IMAGE 'edit.png' CHANGEKEY 'BACK_SPACE' HIDE CHANGEMOUSE 'DBLCLK' TOOLBAR;

filterProject = DATA LOCAL NESTED Project ();
nameFilterProject 'Проект' = name(filterProject());

filterRepresentative = DATA LOCAL NESTED Employee ();
nameFilterRepresentative 'Наш представитель' = MasterData.overName(filterRepresentative());
filterStatus = DATA BillStatus ();
nameFilterStatus 'Статус оплаты' = name(filterStatus());
filterUrgent 'Срочные' = DATA LOCAL BOOLEAN ();

backgroundColor = ABSTRACT CASE COLOR (Bill);
backgroundColor(Bill b) += WHEN isUrgent(b) THEN RGB(237,193,116);

FORM billDashboard 'Счета на услуги'
    OBJECTS bl = Bill BACKGROUND backgroundColor(bl)
    PROPERTIES(bl) READONLY number, date, imagedNameStatus BACKGROUND colorStatus(bl), overNameVendor,
                            items, nameProject, note, overNameRepresentative, amount
    PROPERTIES(bl) 'Счет' = openFiles GRID, cancel SHOWIF status(bl) = BillStatus.draft
    PROPERTIES newLegalEntityBill() DRAW bl TOOLBAR
    PROPERTIES(bl) NEWSESSION editBillCustom, DELETE
    FILTERS legalEntity(bl), billService(type(bl)),
            representative(bl) = currentUser() OR showAllInvoices(currentUser()) OR NOT representative(bl)

    OBJECTS bi 'Оплата freelance' = Bill BACKGROUND backgroundColor(bi)
    PROPERTIES(bi) READONLY number, date, imagedNameStatus BACKGROUND colorStatus(bi), overNameVendor, 
                            items, nameProject, note, overNameRepresentative, amount
    PROPERTIES(bi) 'Счет' = openFiles GRID, cancel SHOWIF status(bi) = BillStatus.draft
    PROPERTIES newIndividualBill() DRAW bi TOOLBAR
    PROPERTIES(bi) NEWSESSION editBillCustom, DELETE
    FILTERS individual(bi), billService(type(bi)),
            representative(bi) = currentUser() OR showAllInvoices(currentUser()) OR NOT representative(bi)

    PROPERTIES() nameFilterProject, nameFilterRepresentative, nameFilterStatus, filterUrgent
    FILTERS project(bl) = filterProject() OR NOT filterProject(), 
        project(bi) = filterProject() OR NOT filterProject(),
        representative(bl) = filterRepresentative() OR NOT filterRepresentative(), 
        representative(bi) = filterRepresentative() OR NOT filterRepresentative(),
        status(bl) = filterStatus() OR NOT filterStatus(), status(bi) = filterStatus() OR NOT filterStatus(),
        isUrgent(bl) OR NOT filterUrgent(), isUrgent(bi) OR NOT filterUrgent()
;

DESIGN billDashboard {
    OBJECTS {
        NEW filters {
            caption = '';
            border = FALSE;
            alignment = CENTER;
            horizontal = TRUE;
            MOVE PROPERTY (nameFilterProject());
            MOVE PROPERTY (nameFilterRepresentative());
            MOVE PROPERTY (nameFilterStatus());
            MOVE PROPERTY (filterUrgent());
        }
        NEW pane{
            fill = 1;
            NEW billLegalEntity {
                caption = 'Счета на оплату';
                fill = 1;
                NEW headerLegalEntity {
                    horizontal = TRUE;
                    alignment = STRETCH;
                    MOVE TOOLBAR(bl);
                    NEW actionsLegalEntity {
                        fill = 1;
                        NEW markLegalEntity {
                            horizontal = TRUE;
                            alignment = END;
                            MOVE PROPERTY (cancel(bl));
                        }
                    }
                }
                MOVE BOX(bl) { caption = ''; };
            }
            NEW billIndividual {
                caption = 'Оплата freelance';
                fill = 1;
                NEW headerIndividual {  
                    horizontal = TRUE;
                    alignment = STRETCH;
                    MOVE TOOLBAR(bi);
                    NEW actionsIndividual {
                        fill = 1;
                        NEW markIndividual {
                            horizontal = TRUE;
                            alignment = END;
                            MOVE PROPERTY (cancel(bi));
                        }
                    }
                }
                MOVE BOX(bi) { caption = ''; }
            }
        }
    }
}

NAVIGATOR {
    dashboard {
        NEW billDashboard;
    }
}