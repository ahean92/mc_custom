MODULE ProjectTimeEntry;

REQUIRE Project, TimeEntry;

NAMESPACE ProjectManagement;

project = DATA Project (TimeEntry);
nameProject 'Project' (TimeEntry e) = name(project(e));
nameCustomer 'Partner' (TimeEntry e) = namePartner(project(e));

countTimeEntry 'Time entries' (Project p) = GROUP SUM 1 IF project(TimeEntry t) = p;  
hoursTimeEntry 'Hours' (Project p) = GROUP SUM hours(TimeEntry t) IF project(t) = p;  

EXTEND FORM timeEntry
    PROPERTIES(t) nameProject
;

DESIGN timeEntry {
    header {
        MOVE PROPERTY(nameProject(t));
    }
}

EXTEND FORM timeEntries
    PROPERTIES(t) READONLY nameProject
;

EXTEND FORM project
    OBJECTS te = TimeEntry
    PROPERTIES(te) nameEmployee, date, hours, description, NEW, DELETE
    FILTERS project(te) = p
;

DESIGN project {
    details {
        NEW timeEntries {
            caption = badged('Time entries', '(' + countTimeEntry(p) + ')' + '(' + hoursTimeEntry(p) + ')');
            MOVE BOX(te) { caption = ''; }
        }
    }
}

EXTEND FORM projects
    PROPERTIES(p) READONLY countTimeEntry, hoursTimeEntry
;