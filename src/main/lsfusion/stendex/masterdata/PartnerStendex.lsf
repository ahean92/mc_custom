MODULE PartnerStendex;

REQUIRE Partner, LegalEntityRu, EmployeeStendex, DaDataLegalEntityFind, PartnerPurchase, PartnerSales;

NAMESPACE MasterData;

isOrganizer 'Организатор' = DATA BOOLEAN (Partner);

WHEN SET(isOrganizer(Partner p)) DO {
    isVendor(p) <- NULL;
    isCustomer(p) <- NULL;
}

EXTEND FORM partners
    PROPERTIES(p) READONLYIF isReadonly() isOrganizer
;

EXTEND FORM partner 
    PROPERTIES(p) isOrganizer
;

DESIGN partner {
    headerColumn3 {
        MOVE PROPERTY(isOrganizer(p));    
    }
}

codeName 'Кодовое название' = DATA ISTRING[50] (Partner) CHARWIDTH 15;
overName '{Name}' (Partner p) = OVERRIDE codeName(p), overName[Individual](p), name(p) CHARWIDTH 15;

partnerByAnyName(STRING s) = GROUP MIN Partner v IF s == name(v) OR s == fullName(v) OR s == codeName(v);
anyNamePartner(STRING anyNamePartner) = anyNamePartner IF GROUP MIN Partner p IF anyNamePartner == name(p) OR anyNamePartner == fullName(p) OR anyNamePartner == codeName(p);

EXTEND FORM partner
    PROPERTIES(p) codeName
;

DESIGN partner {
    headerDetails {
        MOVE PROPERTY(codeName(p));
    }
}

EXTEND FORM employee
    PROPERTIES(e) codeName
;

DESIGN employee {
    info {
        MOVE PROPERTY(codeName(e)) AFTER PROPERTY(nameLegalEntity(e));
    }
}

EXTEND FORM partners
    PROPERTIES(p) READONLYIF isReadonly() codeName AFTER name(p)
    FILTERGROUP type
        FILTER 'Legal entities' p IS LegalEntity
        FILTER 'Individuals' p IS Individual AND NOT p IS Employee
        FILTER 'Employees' p IS Employee
;

codeNameFilter '' = DATA LOCAL ISTRING () CHARWIDTH 20;

EXTEND FORM dialogPartners
    PROPERTIES(p) READONLY codeName
    
    PROPERTIES() codeNameFilter
    FILTERS isISubstring(codeName(p), codeNameFilter()) OR NOT codeNameFilter()
    
    EVENTS ON INIT { codeNameFilter() <- NULL; }
;

DESIGN dialogPartners {
    filters {
        PROPERTY (code()) {caption = 'Поиск по ИНН/ОГРН';}
        MOVE PROPERTY(codeNameFilter())  { placeholder = 'Кодовое название'; }
    }
}

DESIGN partner {       
    headerColumn1 {
        PROPERTY(address(p)) {caption = 'Почтовый адрес';};
    }
    headerColumn2 {
        MOVE PROPERTY(inn(p));
    }
}


DESIGN partner {
    header {
        alignment = STRETCH;
        headerRight {
            fill = 1;
        };
    }
}