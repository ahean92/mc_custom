MODULE PurchaseOrderStendex;

REQUIRE PurchaseOrderSearch, ProjectPurchaseOrder, PurchaseOrderConfirmed, PurchaseOrderLocked, EmployeeStendex;

NAMESPACE Purchase;

overNameRepresentative 'Our representative' (Order o) = OVERRIDE MasterData.overName(representative(o)), MasterData.name(representative(o));
defaultTaxRate  = DATA Tax ();
nameDefaultTaxRate 'Налог по умолчанию' = name(defaultTaxRate());

EXTEND FORM options
    PROPERTIES nameDefaultTaxRate()
;

WHEN LOCAL SETCHANGED(item(OrderLine l)) AND taxIncluded(l) DO in(l, defaultTaxRate()) <- TRUE;
WHEN LOCAL SETCHANGED(type(Order o)) AND NOT taxIncluded(type(o)) DO in(OrderLine l, Tax t) <- NULL WHERE order(l) = o;
WHEN LOCAL SETCHANGED(type(Order o)) AND taxIncluded(type(o)) DO in(OrderLine l, defaultTaxRate()) <- TRUE WHERE order(l) = o AND NOT(GROUP SUM 1 IF in(l, Tax t));

EXTEND FORM order
    PROPERTIES(o) overNameRepresentative
    PROPERTIES (l) 'Сумма налогов' = taxAmount 
;
DESIGN order {
    purchaseInformation {
        MOVE PROPERTY(overNameRepresentative(o)) AFTER PROPERTY(nameRepresentative(o));
        REMOVE PROPERTY(nameRepresentative(o));
    }
}

EXTEND FORM orders
    PROPERTIES(o) READONLYIF isReadonly() overNameRepresentative AFTER nameRepresentative(o)
;
DESIGN orders {
    PROPERTY(nameRepresentative(o)) { showIf = NULL; }
}

dataColor 'Цвет' = DATA COLOR (OrderStatus);
caption 'Подпись' = DATA STRING[50] (OrderStatus);
captionButton (OrderStatus s) = OVERRIDE caption(s), name(s);

useCustomStatus = ABSTRACT BOOLEAN ();
useCustomStatus() += TRUE;

dataStatus = DATA OrderStatus (Order);
status(Order o) += WHEN useCustomStatus() AND dataStatus(o) THEN dataStatus(o);
dateTime = DATA DATETIME(Order, OrderStatus);

WHEN SETCHANGED(status(Order o)) DO {
    dateTime(o,status(o)) <- currentDateTime();
}

allowPrevStatusSet 'Разрешить возвращать статус заказа' = DATA BOOLEAN (UserRole);
allowPrevStatusSet (User u) = GROUP SUM 1 IF allowPrevStatusSet(UserRole r) AND has(u, r);

EXTEND FORM securityPolicy
    PROPERTIES(ur) PANEL allowPrevStatusSet
;
DESIGN securityPolicy {
    roleApplicationSettings {
        tabbed = TRUE;
        NEW order {
            caption = 'Purchase order';
            lines = 3;
            MOVE PROPERTY(allowPrevStatusSet(ur));
        }
    }
}

allowStatusSet 'Разрешить' = DATA BOOLEAN (OrderStatus, UserRole);


EXTEND FORM Purchase.options
    OBJECTS s = OrderStatus
    PROPERTIES (s) name, imagedName, dataColor, caption
    
    OBJECTS role = UserRole
    PROPERTIES allowStatusSet(s, role), name(role) READONLY 
;

DESIGN Purchase.options{
    tabbedPane{
        NEW orderStatus{
            caption = 'Статусы заказа';
            horizontal = TRUE;
            MOVE BOX (s){
                fill = 0.8;
            }
            MOVE BOX (role) {
                fill = 0.2;
            }
        }
    }
}

//прикрепление файлов
CLASS TypeFile{
    paymentOrder 'Платежное поручение',
    invoice 'Счет',
    consignment 'Накладная',
    other 'Другое'
}

type = DATA TypeFile (OrderFile) NONULL;
nameType 'Тип' (OrderFile f) = staticCaption(type(f));
WHEN SET (OrderFile f IS OrderFile) AND NOT type(f) DO type(f) <- TypeFile.other;


isPP (OrderFile f) = type(f) = TypeFile.paymentOrder;
amount 'Сумма' = DATA NUMERIC[16,2](OrderFile);
overDescription (OrderFile f) = CONCAT ' /', description(f), 'Сумма: ' + amount(f);

amountPayed 'Оплачено' (Order o) = GROUP SUM amount(OrderFile f) IF isPP(f) BY order(f);
//CONSTRAINT SET (OrderFile f IS OrderFile) AND isPP(f) AND NOT amount(f) MESSAGE 'При загрузке платежного поручения необходимо ввести сумму';

payedPartial (Order o) = amountPayed(o) AND untaxedAmount(o) AND amountPayed(o) < untaxedAmount(o);
payed (Order o) = amountPayed(o) AND untaxedAmount(o) AND amountPayed(o) >= untaxedAmount(o);
invoice 'Счет' (Order o) = GROUP LAST OrderFile f ORDER dateTime(f) WHERE type(f) = TypeFile.invoice AND order(f) = o;
openInvoice 'Счет' (Order f) { open(file(invoice(f)), nameExtension(invoice(f))); } IMAGE 'open.png';
fileInvoice (Purchase.Order o) = file(invoice(o));

consignment 'Накладная' (Order o) = GROUP LAST OrderFile f ORDER dateTime(f) WHERE type(f) = TypeFile.consignment AND order(f) = o;
openConsignment 'Накладная' (Order f) { open(file(consignment(f)), nameExtension(invoice(f))); } IMAGE 'open.png';
fileConsignment (Purchase.Order o) = file(consignment(o));
readonly = ABSTRACT CASE BOOLEAN (OrderFile);

EXTEND FORM order
    PROPERTIES (of) nameType FIRST READONLYIF readonly(of)
;
EXTEND FORM orders
    FILTERGROUP statusCustom
        FILTER 'Draft' status(o) = OrderStatus.draft
        FILTER 'Отменен' status(o) = OrderStatus.canceled
;

changeStatus ABSTRACT LIST (Order, OrderStatus);
changeStatus (Order o, OrderStatus s) +{
    APPLY;
    IF canceled() THEN RETURN;
}
META defineStatusOrder(name, caption)
EXTEND CLASS OrderStatus{
    name caption
}

changeStatus (Order o, OrderStatus s) +{
    IF s = OrderStatus.##name THEN NEWSESSION {
        dataStatus(o) <- s;
        APPLY;
    }
}
EXTEND FORM orders
    EXTEND FILTERGROUP statusCustom
        FILTER caption status(o) = OrderStatus.name
;
END

@defineStatusOrder(inPayment, 'В оплате');
color(OrderStatus o) += WHEN o = OrderStatus.inPayment THEN RGB(255, 255, 51);


@defineStatusOrder(partiallyPaid, 'Оплачено частитчно');
color(OrderStatus o) += WHEN o = OrderStatus.partiallyPaid THEN RGB(255, 204, 102);

@defineStatusOrder(awaitingDelivery, 'Ждем доставки');
color(OrderStatus o) += WHEN o = OrderStatus.awaitingDelivery THEN RGB(204, 255, 204);

@defineStatusOrder(closed, 'Закрыт');
color(OrderStatus o) += WHEN o = OrderStatus.closed THEN RGB(102, 255, 102);

@defineStatusOrder(review, 'На рассмотрении');

@defineStatusOrder(delivered, 'Доставлен');
WHEN SETCHANGED (dataStatus(Order o) = OrderStatus.delivered)  DO confirmed(o) <- TRUE;

changeStatus (Order o, OrderStatus s) +{
    IF s = OrderStatus.draft THEN  NEWSESSION {
        dataStatus(o) <- s;
        APPLY;
    }
}

changeStatus (Order o, OrderStatus s) +{
    IF s = OrderStatus.canceled THEN  NEWSESSION {
        dataStatus(o) <- s;
        APPLY;
    }
}
showStatusRule = ABSTRACT CASE BOOLEAN (Order, OrderStatus);
showStatusRule(Order o, OrderStatus s) += WHEN status(o) = OrderStatus.draft AND ((s = OrderStatus.review AND invoice(o)) OR s = OrderStatus.canceled) THEN TRUE;
showStatusRule(Order o, OrderStatus s) += WHEN status(o) = OrderStatus.review AND (s = OrderStatus.inPayment OR s = OrderStatus.canceled) THEN TRUE;
showStatusRule(Order o, OrderStatus s) += WHEN status(o) = OrderStatus.partiallyPaid AND (( s = OrderStatus.awaitingDelivery AND payed(o)) OR s = OrderStatus.canceled) THEN TRUE;
showStatusRule(Order o, OrderStatus s) += WHEN status(o) = OrderStatus.inPayment AND ((s = OrderStatus.partiallyPaid AND payedPartial(o)) OR (s = OrderStatus.awaitingDelivery AND payed(o))OR s = OrderStatus.canceled) THEN TRUE;
showStatusRule(Order o, OrderStatus s) += WHEN status(o) = OrderStatus.awaitingDelivery AND (s = OrderStatus.delivered OR s = OrderStatus.canceled) THEN TRUE;
showStatusRule(Order o, OrderStatus s) += WHEN status(o) = OrderStatus.delivered AND ((s = OrderStatus.closed AND consignment(o)) OR s = OrderStatus.canceled) THEN TRUE;
showStatus (Order o, OrderStatus s) = showStatusRule(o, s) AND (GROUP SUM 1 IF allowStatusSet(s, UserRole r) AND in(currentUser(), r));

changeToPrevStatus (Order o, OrderStatus s) {
    dateTime(o,status(o)) <- NULL;
    changeStatus(o,s);
}
prevStatus (Order o) = GROUP LAST OrderStatus s IF dateTime(o,s) < dateTime(o,status(o)) ORDER dateTime(o,s), s;
showPrevStatus (Order o, OrderStatus s) = prevStatus(o) == s AND allowPrevStatusSet(currentUser());

color(Order o, OrderStatus s) = IF o IS Order THEN color(s);
uploadOtherFile 'Файл' (Order o){
    INPUT uf = NAMEDFILE DO NEW uof = OrderFile {
        order(uof) <- o;
        file(uof) <- RAWFILE(uf);
        name(uof) <- name(uf);
        extension(uof) <- extension(uf);
        type(uof) <- TypeFile.other;
    }
} IMAGE 'add.png' CHANGEKEY 'INSERT' HIDE;

FORM inputOrderOtherFiles 'Добавление файлов'
    OBJECTS o = Order PANEL

    OBJECTS of = OrderFile
    PROPERTIES(of) READONLYIF readonly(o) name, extension, description
    PROPERTIES(of) READONLY nameUser, dateTime
    PROPERTIES(of) GRID open, DELETE READONLYIF readonly(o)
    FILTERS order(of) = o,
            type(of) = TypeFile.other

    PROPERTIES(o) uploadOtherFile DRAW of TOOLBAR READONLYIF readonly(o)
    PROPERTIES inputFile = '' CUSTOM 'inputFile' ON CHANGE {
        INPUT f = JSON DO
            IMPORT JSON FROM f FIELDS() STRING name, STRING extension, STRING data DO
                IF NOT readonly(o) THEN NEW uof = OrderFile {
                    order(uof) <- o;
                    file(uof) <- decode(data, 'base64');
                    name(uof) <- name;
                    extension(uof) <- extension;
                    type(uof) <- TypeFile.other;
                }
    }
;

DESIGN inputOrderOtherFiles {
    size = (800, 600);
    BOX(of) {
        caption = '';
        MOVE PROPERTY(inputFile);
    }
}

newOtherFile 'Файл' (Order o) {
    NESTEDSESSION {
        DIALOG inputOrderOtherFiles OBJECTS o = o FLOAT;
    }
} IMAGE 'add.png' CHANGEKEY 'INSERT' HIDE;

EXTEND FORM order
    OBJECTS ps = OrderStatus
    PROPERTIES (o, ps) changeToPrevStatus COLUMNS (ps) HEADER captionButton(ps) BACKGROUND dataColor(ps) SHOWIF countOrderLine(o)
    FILTERS showPrevStatus(o, ps)
    
    OBJECTS s = OrderStatus
    PROPERTIES (o, s) changeStatus COLUMNS (s) HEADER captionButton(s) BACKGROUND dataColor(s) SHOWIF countOrderLine(o)
    FILTERS showStatus(o, s)
    PROPERTIES newOtherFile(o) TOOLBAR DRAW of
;

DESIGN order{
    REMOVE statusActions;
    REMOVE status;
    REMOVE PROPERTY (newFile(o));
    header{
        horizontal = FALSE;
        headerLeft{
            horizontal = TRUE;
            MOVE PROPERTY (imagedNameStatus(o));
            NEW statusButton{
                horizontal = TRUE;
                caption = 'Действия';
                MOVE PROPERTY (changeToPrevStatus(o, ps));
                MOVE PROPERTY (changeStatus(o, s));
            }
        }
        headerRight{
            horizontal = TRUE;
        }
    }
    relatedDoc{
        fill = 0;
    }
    details{
        MOVE BOX(of) AFTER lines;
        MOVE search AFTER BOX(of);
        lines{
            caption = badged('Товары', countOrderLine(o));
        }
    }
}

showPanel = DATA LOCAL BOOLEAN ();

showPanelControl(){
    showPanel() <- NOT showPanel();
}
EXTEND FORM orders
    PROPERTIES () showPanelControl TOOLBAR DRAW o
;

DESIGN orders{
    REMOVE PROPERTY (nameProject(o));
    REMOVE FILTERGROUP (status);
    REMOVE FILTERGROUP (unlocked);
    PROPERTY (showPanelControl()) {
        caption = IF showPanel() THEN 'Скрыть фильтры' ELSE 'Фильтры';
    }
    rightPane{
        showIf = showPanel();
    }
}
CONSTRAINT SETCHANGED (vendor(Order o)) AND vendor(o) IS Employee CHECKED BY vendor[Order] 
    MESSAGE 'Сотрудник не может быть поставщиком';

//WHEN SETCHANGED (isPP(OrderFile f)) AND amountPayed(order(f)) < untaxedAmount(order(f)) AND status(order(f)) = OrderStatus.inPayment 
//    DO changeStatus(order(f), OrderStatus.partiallyPaid);
//
//WHEN SETCHANGED (isPP(OrderFile f)) AND amountPayed(order(f)) >= untaxedAmount(order(f)) 
//    AND (status(order(f)) = OrderStatus.inPayment OR status(order(f)) = OrderStatus.partiallyPaid)
//    DO changeStatus(order(f), OrderStatus.awaitingDelivery);

changedLines (Order o) = [GROUP SUM 1 IF SETCHANGED (item(OrderLine l)) OR SETCHANGED (quantity(l)) OR SETCHANGED (price(l)) BY order(l)](o);

//CONSTRAINT LOCAL CHANGED(changedLines(Order o)) AND invoice(o) MESSAGE 'Нельзя изменять документ после прикрепления счета';

showLines 'Показать состав' = DATA LOCAL NESTED BOOLEAN ();

EXTEND FORM orders
    PROPERTIES showLines() TOOLBAR DRAW o
    
    OBJECTS l = OrderLine
    PROPERTIES(l) READONLY SHOWIF showLines() index, nameItem, description, nameUom, idBarCodeItem, idItem, referenceItem, quantity, price, untaxedAmount, taxes
    FILTERS order(l) = o
;

DESIGN orders {
    tabbedPane {
        NEW orders {
            caption = 'Purchase orders';
            MOVE BOX(o) {
                TOOLBARBOX(o) {
                    MOVE PROPERTY(showLines()) BEFORE TOOLBARRIGHT(o);
                }
            }
            MOVE BOX(l);
        }
    }
}

payBeforeDate 'Оплатить до' = DATA DATE (Order);

EXTEND FORM order
    PROPERTIES(o) payBeforeDate
;
DESIGN order {
    otherInformation {
        NEW billInformation {
            caption = 'Оплата';
            alignment = STRETCH;
            MOVE PROPERTY(payBeforeDate(o));
        }
    }
}

EXTEND FORM orders
    PROPERTIES(o) READONLYIF isReadonly() payBeforeDate

    PROPERTIES order 'Порядок' = LONG(o) SHOWIF NULL
    ORDERS order DESC
;
