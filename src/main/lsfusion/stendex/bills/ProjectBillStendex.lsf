MODULE ProjectBillStendex;

REQUIRE Project, BillPaymentStendex, ProjectStendex;

NAMESPACE Project;

EXTEND FORM changeBillProjects
    PROPERTIES(p) READONLY overNameManager
;
EXTEND FORM bill
    PROPERTIES(p) READONLY overNameManager BEFORE amount(b,p)
;
EXTEND FORM billCustom
    PROPERTIES(pr) READONLY overNameManager BEFORE amount(b,pr)
;

newLegalEntityBill 'Добавить для организаций' (Project p) {
    NEWSESSION {
        NEW b = Bill {
            type(b) <- legalEntityType();
            in(b,p) <- TRUE;
            SHOW billCustom OBJECTS b = b;
        }
    }
}

newIndividualBill 'Добавить для freelance' (Project p) {
    NEWSESSION {
        NEW b = Bill {
            type(b) <- individualType();
            in(b,p) <- TRUE;
            SHOW billCustom OBJECTS b = b;
        }
    }
}

EXTEND FORM project
    OBJECTS bb = Bill
    PROPERTIES(bb) READONLY number, dateTime,
        imagedNameStatus BACKGROUND colorStatus(bb), nameType,
        dueDateTime, namePaymentTerms
    PROPERTIES(bb,p) READONLY amount
    FILTERS in(bb,p)
    
    OBJECTS bl 'Счета на оплату' = Bill BACKGROUND backgroundColor(bl)
    PROPERTIES(bl) READONLY number, date, items, nameType, nameProjects, note, overNameRepresentative, amount, left, toPay, paidDone
    PROPERTIES(bl,p) READONLY amount
    PROPERTIES(bl) 'Счет' = openFiles GRID
    PROPERTIES READONLYIF readonly(p) newLegalEntityBill(p) DRAW bl TOOLBAR
    PROPERTIES READONLYIF readonly(p) newIndividualBill(p) DRAW bl TOOLBAR
    PROPERTIES(bl) READONLYIF readonly(p) NEWSESSION editBillCustom, DELETE
    FILTERS legalEntity(bl) OR individual(bl), in(bl,p), billService(type(bl))   
;

countBillsProject 'Bills' (Project p) = GROUP SUM 1 IF in(Bill b, p);
countBillsStendex 'Bills' (Project p) = GROUP SUM 1 IF in(Bill b, p) AND billService(type(b));

DESIGN project {
    details {
        REMOVE BOX(b);
        MOVE BOX(bb) {
            caption = badged('Bills', countBillsProject(p));
        }
        NEW bills {
            caption = badged('Счета', countBillsStendex(p));
            MOVE BOX(bl);
        }
    }
}