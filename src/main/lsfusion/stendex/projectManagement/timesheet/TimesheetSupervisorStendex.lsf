MODULE TimesheetSupervisorStendex;

REQUIRE TimesheetSupervisor, EmployeeStendex, TimeEntryStendex;

PRIORITY MasterData;

NAMESPACE ProjectManagement;

hours (Employee e, TimeEntryHours h, DATE dFrom, DATE dTo) = 
    GROUP SUM hours(TimeEntry te) IF h == timeEntryHours(te) AND NOT date(te) < dFrom AND NOT date(te) > dTo AND employee(te) == e AND 
        (project(te) = timesheetProject() OR NOT timesheetProject());

hours 'Hours' (Employee e, TimeEntryHours h, INTERVAL[DATE] dates) = hours(e, h, from(dates), to(dates));

FORM dialogEmployeeTimeEntryHours 'Employee'
    OBJECTS dates = INTERVAL[DATE] PANEL NULL    
    OBJECTS e = Employee PANEL
    PROPERTIES (e) READONLY lastName, firstName, middleName, photo, passportInformation, tariff
    PROPERTIES (e) openScanFile
       
    OBJECTS h = TimeEntryHours
    PROPERTIES(h) name
    PROPERTIES(e, h, dates) hours
    FILTERS hours(e, h, dates)
;

DESIGN dialogEmployeeTimeEntryHours {
    OBJECTS {
        NEW header {
            alignment = STRETCH;
            horizontal = TRUE;
            fill = 1;

            MOVE PROPERTY(photo(e)) {
                fill = 1;
                width = 100;
                caption = '';
            }

            NEW info {
                fill = 2;
                MOVE PROPERTY(lastName(e));
                MOVE PROPERTY(firstName(e));
                MOVE PROPERTY(middleName(e));
                MOVE PROPERTY(passportInformation(e));
                MOVE PROPERTY(passportInformation(e));
                MOVE PROPERTY(openScanFile(e));
                MOVE PROPERTY(tariff(e));
            }
        }
        NEW hours {
            fill = 1;
            MOVE BOX(h) {
                fill = 2; 
                caption = '';
            } 
        }
    }
}

EXTEND FORM timesheetSupervisor
    PROPERTIES photo(e) ON CHANGE { SHOW dialogEmployeeTimeEntryHours OBJECTS dates = dates, e = e  FLOAT;} FIRST
    PROPERTIES firstName '{First_name}' = firstName(e) ON CHANGE { SHOW dialogEmployeeTimeEntryHours OBJECTS dates = dates, e = e  FLOAT;} AFTER photo(e)
;

DESIGN timesheetSupervisor {
    PROPERTY(firstName(e)) { hide = TRUE; }
}

trunc = FORMULA INTEGER 'trunc($1)';
hoursTimesheet 'Часы' (DATE d, Employee e, Workplace w) = GROUP SUM hours(TimeEntry t) IF date(t) = d AND employee(t) = e AND workplace(t) = w;
overtimeHours 'Часы переработки' (DATE d, Employee e) = GROUP SUM overtimeHours(TimeEntry t) IF date(t) = d AND employee(t) = e;
descriptionWorkplaceTimesheet 'Время работы' (DATE d, Employee e) = 
    GROUP CONCAT (CONCAT '', 
        symbol(Workplace w), 
        '(', IF hoursTimesheet(d, e, w) - trunc(hoursTimesheet(d, e, w)) < 0.001 THEN trunc(hoursTimesheet(d, e, w)) ELSE hoursTimesheet(d, e, w), ')') IF hoursTimesheet(d, e, w), '+' ORDER w; 

descriptionTimesheet 'Время работы' (DATE d, Employee e) = 
     OVERRIDE 
        (CONCAT '+', descriptionWorkplaceTimesheet(d, e), IF overtimeHours(d, e) - trunc(overtimeHours(d, e)) < 0.001 THEN trunc(overtimeHours(d, e)) ELSE overtimeHours(d, e)), 
        STRING(IF hoursTimesheet(d,e) - trunc(hoursTimesheet(d,e)) < 0.001 THEN trunc(hoursTimesheet(d,e)) ELSE hoursTimesheet(d,e));

breakInterval 'Интервал перерыва' = DATA INTERVAL[TIME] ();
fromBreakTimeDefault 'с' = DATA TIME ();
toBreakTimeDefault 'по' = DATA TIME ();


EXTEND FORM options PROPERTIES() fromBreakTimeDefault, toBreakTimeDefault;

DESIGN options {
    commons {
        NEW breakInterval {
            horizontal = TRUE;
            caption = 'Интервал перерыва';            
            MOVE PROPERTY(fromBreakTimeDefault());
            MOVE PROPERTY(toBreakTimeDefault());
        }
    }
}

//workInterval 'Интервал работы' = DATA LOCAL NESTED INTERVAL[TIME] ();

overtimeHours 'Часы переработки' = DATA LOCAL NESTED NUMERIC[8,2]();

workplace 'Расположение' = DATA LOCAL NESTED Workplace ();
nameWorkplace 'Расположение' () = name(workplace());

timeEntryHours 'Смена' = DATA LOCAL NESTED TimeEntryHours ();
nameTimeEntryHours 'Смена' () = name(timeEntryHours());

hours 'Hours' (TIME from, TIME to) = NUMERIC[8,2](extractHour(to) (+) NUMERIC[8,2](extractMinute(to))/60  (-) extractHour(from) (-) NUMERIC[8,2](extractMinute(from))/60);

workInterval 'Интервал работы' = DATA INTERVAL[TIME] (TimeEntry);
breakInterval 'Интервал перерыва' = DATA INTERVAL[TIME] (TimeEntry);

fromWorkTime 'с' = DATA TIME (TimeEntry);
toWorkTime 'по' = DATA TIME (TimeEntry);
fromBreakTime 'с' = DATA TIME (TimeEntry);
toBreakTime 'по' = DATA TIME (TimeEntry);

fromWorkTime 'с' = DATA LOCAL NESTED TIME ();
toWorkTime 'по' = DATA LOCAL NESTED TIME ();

dataFromBreakTime 'с' = DATA LOCAL NESTED TIME ();
dataToBreakTime 'по' = DATA LOCAL NESTED TIME ();

time(STRING time) = TIME(toDateTimeFormat(time,'hh24:mi'));
//timeFromHours (INTEGER hour) = TIME(toDateTimeFormat(lpad(STRING(hour),2,'0')+'00','HH24MI'));
fromBreakTime 'с' () = OVERRIDE dataFromBreakTime(), fromBreakTimeDefault(), time('13:00');
toBreakTime 'по' () = OVERRIDE dataToBreakTime(), toBreakTimeDefault(), time('14:00');

hoursIntervalSipmple(TIME fromWorkTime, TIME toWorkTime, TIME fromBreakTime, TIME toBreakTime) = CASE
        WHEN fromWorkTime <= fromBreakTime AND toWorkTime >= toBreakTime THEN hours(fromWorkTime, toWorkTime) (-) hours(fromBreakTime, toBreakTime)
        WHEN fromWorkTime > fromBreakTime AND fromWorkTime < toBreakTime THEN hours(toBreakTime, toWorkTime)
        WHEN toWorkTime < toBreakTime AND fromBreakTime < toWorkTime THEN hours(fromWorkTime, fromBreakTime)
        WHEN toBreakTime < toWorkTime AND fromBreakTime > toWorkTime THEN NULL
        ELSE hours(fromWorkTime, toWorkTime);

hoursInterval(TIME fromWorkTime, TIME toWorkTime, TIME fromBreakTime, TIME toBreakTime) = CASE                                                                                              
    WHEN fromWorkTime > toWorkTime THEN hoursIntervalSipmple(fromWorkTime, time('23:59'), fromBreakTime, toBreakTime) + 0.02 (+) hoursIntervalSipmple(time('00:00'), toWorkTime, fromBreakTime, toBreakTime) 
    ELSE hoursIntervalSipmple(fromWorkTime, toWorkTime, fromBreakTime, toBreakTime);

hoursInterval(TimeEntry te) = hoursInterval(fromWorkTime(te), toWorkTime(te), fromBreakTime(te), toBreakTime(te));

WHEN LOCAL SETCHANGED(fromWorkTime(TimeEntry te)) OR SETCHANGED(toWorkTime(te)) OR SETCHANGED(fromBreakTime(te)) OR SETCHANGED(toBreakTime(te)) DO {
    hours(te) <- hoursInterval(te);
}

hoursInterval 'Hours' () = hoursInterval(fromWorkTime(), toWorkTime(), fromBreakTime(), toBreakTime());

FORM dialogWorkInterval 'Отметка времени'
    OBJECTS d = DATE PANEL
    OBJECTS e = Employee PANEL
    OBJECTS et = TimeEntryType PANEL

    PROPERTIES() PANEL
        nameTimeEntryHours, fromBreakTime, toBreakTime, fromWorkTime, toWorkTime, hoursInterval READONLY, overtimeHours, nameWorkplace
;

DESIGN dialogWorkInterval {
    OBJECTS {
        NEW header {
            horizontal = FALSE;

            MOVE PROPERTY(nameTimeEntryHours());
            NEW time {
                NEW breakInterval {
                    horizontal = TRUE;
                    caption = 'Интервал перерыва';
                    MOVE PROPERTY(fromBreakTime());
                    MOVE PROPERTY(toBreakTime());
                }
                NEW workInterval {
                    horizontal = TRUE;
                    caption = 'Интервал работы';
                    MOVE PROPERTY(fromWorkTime());
                    MOVE PROPERTY(toWorkTime());
                    MOVE PROPERTY(hoursInterval());
                }
            }
            MOVE PROPERTY(overtimeHours());         
            MOVE PROPERTY(nameWorkplace());         
        }
    }
}

timeEntry (DATE d, Employee e, Project p, TimeEntryType et, Workplace w) = GROUP MIN TimeEntry t BY date(t), employee(t), project(t), type(t), workplace(t);
timeEntry (DATE d, Employee e, TimeEntryType et, Workplace w) = GROUP MIN TimeEntry t BY date(t), employee(t), type(t), workplace(t);

changeTimesheetHours (DATE d, Employee e, TimeEntryType et, Workplace w, NUMERIC[8,2] n, TimeEntryHours h) {
    IF n THEN {
        IF timesheetProject() THEN {
            FOR TimeEntry t = timeEntry(d, e, timesheetProject(), et, w) DO {
                IF hours(t) = n AND h THEN
                    DELETE t;
                ELSE {
                    hours(t) <- n;
                    timeEntryHours(t) <- h;
                    workplace(t) <- w;
                    fromWorkTime(t) <- fromWorkTime();
                    toWorkTime(t) <- toWorkTime();
                    fromBreakTime(t) <- fromBreakTime();
                    toBreakTime(t) <- toBreakTime();
                    overtimeHours(t) <- overtimeHours();
                }
            } ELSE {
                DELETE TimeEntry te WHERE project(te) = timesheetProject() AND employee(te) = e AND date(te) = d AND workplace(te) = w;
                IF NOT w THEN DELETE TimeEntry te WHERE project(te) = timesheetProject() AND employee(te) = e AND date(te) = d AND NOT workplace(te);
                NEW t = TimeEntry {
                    project(t) <- timesheetProject();
                    type(t) <- et;
                    employee(t) <- e;
                    date(t) <- d;
                    hours(t) <- n;
                    timeEntryHours(t) <- h;
                    workplace(t) <- w;
                    fromWorkTime(t) <- fromWorkTime();
                    toWorkTime(t) <- toWorkTime();
                    fromBreakTime(t) <- fromBreakTime();
                    toBreakTime(t) <- toBreakTime();
                    overtimeHours(t) <- overtimeHours();
                }
            }
        } ELSE {
            FOR TimeEntry t = timeEntry(d, e, et, w) DO {
                IF hours(t) = n AND h THEN
                    DELETE t;
                ELSE {
                    hours(t) <- n;
                    timeEntryHours(t) <- h;
                    workplace(t) <- w;
                    fromWorkTime(t) <- fromWorkTime();
                    toWorkTime(t) <- toWorkTime();
                    fromBreakTime(t) <- fromBreakTime();
                    toBreakTime(t) <- toBreakTime();
                    overtimeHours(t) <- overtimeHours();
                }
            } ELSE {
                DELETE TimeEntry te WHERE NOT project(te) AND employee(te) = e AND date(te) = d AND workplace(te) = w;
                IF NOT w THEN DELETE TimeEntry te WHERE NOT project(te) AND employee(te) = e AND date(te) = d AND NOT workplace(te);
                NEW t = TimeEntry {
                    type(t) <- et;
                    employee(t) <- e;
                    date(t) <- d;
                    hours(t) <- n;
                    timeEntryHours(t) <- h;
                    workplace(t) <- w;
                    fromWorkTime(t) <- fromWorkTime();
                    toWorkTime(t) <- toWorkTime();
                    fromBreakTime(t) <- fromBreakTime();
                    toBreakTime(t) <- toBreakTime();
                    overtimeHours(t) <- overtimeHours();
                }
            }
        }
    } ELSE {
        DELETE TimeEntry t WHERE matchesProject(t) AND employee(t) = e AND date(t) = d AND workplace(t) = w;
        IF NOT w THEN DELETE TimeEntry t WHERE matchesProject(t) AND employee(t) = e AND date(t) = d AND NOT workplace(t);
    }
}

changeSaveTimesheetHours (DATE d, Employee e, TimeEntryType et, Workplace w, NUMERIC[8,2] n, TimeEntryHours h, BOOLEAN autoSave) {
    IF NOT autoSave THEN {
        changeTimesheetHours(d, e, et, w, n, h);
    } ELSE
        NEWSESSION APPLY {
            changeTimesheetHours(d, e, et, w, n, h);
        }
}

EXTEND FORM timesheetSupervisor
    PROPERTIES descriptionTimesheet(d, e) WAIT COLUMNS (d) HEADER extractDay(d) BACKGROUND backgroundHoursTimesheet(d, e)
    ON CHANGE {        
        FOR TimeEntryType et = timesheetTimeEntryType() AND (timesheetProject() OR NOT project(timeEntry(d, e, timesheetTimeEntryType()))) DO {
            timeEntryHours() <- timesheetTimeEntryHours();

            DIALOG dialogWorkInterval OBJECTS d = d, e = e, et = et DO {
                changeSaveTimesheetHours(d, e, et, workplace(), hoursInterval(), timeEntryHours(), autoSaveTimesheetHours());
            };
        } ELSE
        SHOW timesheetSupervisorDateEmployee OBJECTS i = interval(d, d), e = e FLOAT;
    } ON CONTEXTMENU copyHours(d) ON CONTEXTMENU clearHours(d) FOOTER hours(d, timesheetProject())
;

DESIGN timesheetSupervisor {
    OBJECTS {
        tabbedPane {
            timesheet {
                MOVE BOX(e) {                    
                    PROPERTY(hoursTimesheet(d, e)) {hide = TRUE;};
                    PROPERTY(descriptionTimesheet(d, e)){
                        charWidth = 7;
                    }
                }
            }
        }
    }
}

EXTEND FORM timesheetSupervisorDateEmployee 
    PROPERTIES(t) READONLY nameWorkplace
;

EXTEND FORM timeEntry
    PROPERTIES(t) fromWorkTime, toWorkTime, fromBreakTime, toBreakTime
;

DESIGN timeEntry {
    OBJECTS {
        pane {
            time {
                NEW breakInterval {
                    horizontal = TRUE;
                    caption = 'Интервал перерыва';
                    MOVE PROPERTY(fromBreakTime(t));
                    MOVE PROPERTY(toBreakTime(t));                    
                }
                NEW workInterval {
                    horizontal = TRUE;
                    caption = 'Интервал работы';
                    MOVE PROPERTY(fromWorkTime(t));
                    MOVE PROPERTY(toWorkTime(t));
                    MOVE PROPERTY(hours(t));
                }                                
            }
        }
    }
}

EXTEND FORM timesheetSupervisorDateEmployee
    PROPERTIES(t) READONLY BEFORE hours(t) fromWorkTime, toWorkTime, fromBreakTime, toBreakTime
;

migrateTimeWorking = DATA BOOLEAN ();

onStarted() + {
    IF NOT migrateTimeWorking() THEN {
        fromWorkTime(TimeEntry t) <- from(workInterval(t)) WHERE workInterval(t);
        toWorkTime (TimeEntry t) <- to(workInterval(t)) WHERE workInterval(t);
        fromBreakTime (TimeEntry t) <- from(breakInterval(t)) WHERE breakInterval(t);
        toBreakTime (TimeEntry t) <- to(breakInterval(t)) WHERE breakInterval(t);

        migrateTimeWorking() <- TRUE;
    }
}