MODULE ManufacturingOrderStendex;

REQUIRE ManufacturingOrder, UtilsStendex;

NAMESPACE Manufacturing;

link 'Ссылка' = DATA LINK (ConsumedLine) CHARWIDTH 25;

FORM materialRequests 'Заявки'
    OBJECTS o = ManufacturingOrder
    PROPERTIES (o) READONLY 'Дата' = scheduledDate, number, nameType, nameConsumedItem, nameStatus, nameResponsible
    PROPERTIES (o) NEWSESSION NEW, EDIT, DELETE

    LIST ManufacturingOrder OBJECT o
;

@defineDocObjectsForm(materialRequests, o, 'Заявки');
@defineDateFilterForm(materialRequests, o, scheduled);

FORM materialRequest 'Заявка'
    OBJECTS o = ManufacturingOrder PANEL
    PROPERTIES (o) scheduledDateTime, number
    PROPERTIES (o) READONLY nameType, nameStatus,  nameResponsible

    OBJECTS cl = ConsumedLine
    PROPERTIES (cl) NOSELECT idItem, nameItem, nameUom, description, link, 'Количество' = toConsume
    PROPERTIES (cl) NEW, EDIT, DELETE
    FILTERS manufacturingOrder(cl) = o

    OBJECTS manufacturingOrderl = ManufacturingOrderHistory
    PROPERTIES(manufacturingOrderl) READONLY dateTime, nameUser, hostnameComputer, type, description, pdescription = description PANEL
    FILTERS manufacturingOrder(manufacturingOrderl) = o

    EDIT ManufacturingOrder OBJECT o
;

DESIGN materialRequest{
    OBJECTS {
        NEW order{
            NEW main{
                horizontal = TRUE;
                MOVE PROPERTY (nameType(o));
                MOVE PROPERTY (nameStatus(o));
            }
            NEW extra{
                horizontal = TRUE;
                MOVE PROPERTY (number(o));
                MOVE PROPERTY (scheduledDateTime(o));
                MOVE PROPERTY (nameResponsible(o));
            }
        }
        NEW details{
            tabbed = TRUE;
            fill = 1;
            MOVE BOX(cl) {
                GRID(cl){
                    height = 150;
                    PROPERTY (description(cl)) {charWidth = 20; }
                    PROPERTY (nameItem(cl)) {charWidth = 16; }
                }
                caption = 'Материалы';
            }
            NEW history {
                caption = 'History';
                fill = 1;
                
                MOVE BOX(manufacturingOrderl) {
                    fill = 2;
                    caption = '';
                    PROPERTY(description(manufacturingOrderl)) { valueHeight = 18; }
                }
                MOVE PROPERTY(pdescription) { panelCaptionVertical = TRUE; fill = 1; }
            }
        }
    }
}



loadDefaultData () + {
    NEW t = ManufacturingOrderType {
        name(t) <- 'Заявка на материалы';
        id(t) <- 'materialRequest';
        NEW n = Numerator {
            name(n) <- 'Заявка на материалы';
            series(n) <- 'ЗМ';
            minValue(n) <- 1;
            maxValue(n) <- 999999;
            stringLength(n) <- 6;
            numerator(t) <- n;
        }
    }
}

WHEN LOCAL SET (ManufacturingOrder t IS ManufacturingOrder) AND NOT type(t) DO type(t) <- manufacturingOrderType('materialRequest');


NAVIGATOR{
    dashboard {
        NEW materialRequests;
    }
}